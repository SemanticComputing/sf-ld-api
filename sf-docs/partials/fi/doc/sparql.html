<h1>SPARQL-palvelupiste</h1>
<p>
  <a href="https://www.w3.org/TR/sparql11-query/">SPARQL</a> on syntaksiltaan SQL:ää muistuttava RDF-kyselykieli.
  Suositeltu tapa käyttää Semanttisen Finlexin aineistoa on SPARQL-kysely palvelupisteeseen.
  Palvelupiste löytyy osoitteesta <b>http://data.finlex.fi/sparql</b>.
  Sitä voi kokeilla esimerkiksi <a href="http://yasgui.org/#query=PREFIX%20rdf%3A%20%3Chttp%3A%2F%2Fwww.w3.org%2F1999%2F02%2F22-rdf-syntax-ns%23%3E%0APREFIX%20rdfs%3A%20%3Chttp%3A%2F%2Fwww.w3.org%2F2000%2F01%2Frdf-schema%23%3E%0ASELECT%20*%20WHERE%20%7B%0A%20%20%3Fsub%20%3Fpred%20%3Fobj%20.%0A%7D%20%0ALIMIT%2010&endpoint=http%3A%2F%2Fdata.finlex.fi%2Fsparql"><b>YASGUI-työkalulla</b></a>. Ohessa on joitakin esimerkkejä
  SPARQL-kyselyistä, joilla rajapintaa voi kutsua kopioimalla laatikon sisällä olevan tekstin YASGUI:n kyselykenttään ja endpoint-kenttään osoitteeksi
  http://data.finlex.fi/sparql.
</p>
<hr/>

<p>Kysely hakee kymmenen vanhimman (säädösvuoden mukaan) kannasta löytyvän säädöksen URI:n</p>
<pre>
  PREFIX rdf: &lt;http://www.w3.org/1999/02/22-rdf-syntax-ns#\>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>

  # Query : List 10 of the oldest statutes
  SELECT ?s WHERE
  {
   ?s rdf:type sfl:Statute .
  } ORDER BY ?s LIMIT 10
</pre>
<hr/>

<p>Kysely hakee Rikoslain (39/1889) eri versiot</p>
<pre>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>

  # Query : Get different temporal versions of Criminal Code
  SELECT ?document_version ?format ?content
  WHERE {
    # you can replace &lt;http://data.finlex.fi/eli/sd/1889/39> with eg. one of the following:
    # http://data.finlex.fi/eli/sd/1889/39/johtolause
    # http://data.finlex.fi/eli/sd/1889/39/luku/1
    # http://data.finlex.fi/eli/sd/1889/39/luku/1/pykala/1
    # http://data.finlex.fi/eli/sd/1889/39/voimaantulo
   &lt;http://data.finlex.fi/eli/sd/1889/39> sfl:hasVersion ?document_version .

   # lang options: fin,swe
   # format options: txt and html
   BIND(IRI(CONCAT(str(?document_version),"/fin/txt")) AS ?format)

   # replace sfl:text with sfl:html for html version
   ?format sfl:text ?content.
  }
</pre>
<hr/>

<p>Kysely hakee 10 eri suomenkielistä säädöksen nimekettä</p>
<pre>
  PREFIX rdf: &lt;http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>

  # Query : Get 10 legal document titles
  SELECT ?title
  WHERE {
    ?s rdf:type sfl:Statute .
    ?s sfl:hasVersion ?v .
    BIND(IRI(CONCAT(str(?v),"/fin")) AS ?e)
    ?e eli:title ?title .
  } LIMIT 10
</pre>
<hr/>

<p>Kysely hakee Osakeyhtiölain (624/2006) uusimman version</p>
<pre>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>

  # Query : Get the newest version of a statute (Osakeyhtiölaki)
  SELECT ?s WHERE {
    &lt;http://data.finlex.fi/eli/sd/2006/624> sfl:hasVersion ?s .
    OPTIONAL {?s eli:version_date ?dv .}
  }
  ORDER BY DESC(?dv) LIMIT 1
</pre>
<hr/>

<p>Kysely hakee Osakeyhtiölain uusimman version pykälien otsikot suomen kielellä</p>
<pre>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>

  # Query : Get section headings of the newest version of a statute
  SELECT ?t WHERE {
    {
      SELECT ?sv ?dv WHERE
      {
        &lt;http://data.finlex.fi/eli/sd/2008/521> sfl:hasVersion ?sv .
        OPTIONAL { ?sv eli:version_date ?dv .}
      } ORDER BY DESC(?dv) LIMIT 1
    }
    ?sv eli:has_part+ ?sec .
    ?sec a sfl:SectionVersion .
    BIND(IRI(CONCAT(str(?sec),"/fin")) AS ?exp)
    ?exp eli:title ?t .
  }
</pre>
<hr/>

<p>Kysely listaa 10 tuomioistuimen ratkaisua</p>
<pre>
  PREFIX rdf: &lt;http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX sfcl: &lt;http://data.finlex.fi/schema/sfcl/>

  # Query : List judgements
  SELECT ?j WHERE
  {
   ?j rdf:type sfcl:Judgement .
 } LIMIT 10
</pre>
<hr/>

<p>Kysely hakee erään KKO:n ratkaisun suomen kielisen tekstisisällön</p>
<pre>
  PREFIX sfcl: &lt;http://data.finlex.fi/schema/sfcl/>

  # Query : Get judgement content
  SELECT ?document_version ?format ?content
  WHERE {
   # lang options: fin,swe
   # format options: txt, html, xml
   # replace sfcl:text with sfcl:html / sfcl:xml for html / xml version
   &lt;http://data.finlex.fi/ECLI:FI:KKO:2015:65/fin/txt> sfcl:text ?content.
  }
</pre>
<hr/>

<p>Kysely laskee eri tyyppisten lainkohtien kokonaismäärä Rikoslain viimeisestä versiosta</p>
<pre>
  PREFIX rdf: &lt;http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>

  # Query : Count number of different subparts of the latest statute version
  SELECT (COUNT(?o) AS ?co) ?c WHERE {
    {
    	SELECT ?sv WHERE {
        &lt;http://data.finlex.fi/eli/sd/1889/39> sfl:hasVersion ?sv .
        ?sv eli:version_date ?dv .
      } ORDER BY DESC(?dv) LIMIT 1
    }
    ?sv eli:has_part+ ?o.
    ?o rdf:type ?c
  } GROUP BY ?c
</pre>
<hr/>

<p>Kysely hakee viittaukset muihin säädöksiin Rikoslain uusimmasta versiosta</p>
<pre>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>

  # Query : Find references to other statutes from latest statute version
  SELECT ?o ?r WHERE {
    # select latest
    {
      SELECT ?sv WHERE {
        &lt;http://data.finlex.fi/eli/sd/1889/39> sfl:hasVersion ?sv .
        ?sv eli:version_date ?dv .
      } ORDER BY DESC(?dv) LIMIT 1
    }
    # get all parts
    ?sv eli:has_part+ ?o.
    # get references
    ?o eli:cites ?r
  }
</pre>
<hr/>

<p>Kysely hakee asiasanat Rikoslain uusimmasta versiosta suomen kielellä</p>
<pre>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>
  PREFIX skos: &lt;http://www.w3.org/2004/02/skos/core#>

  # Query : Find keywords by statute
  SELECT ?sv ?c ?l WHERE {
    {
      SELECT ?sv WHERE {
        &lt;http://data.finlex.fi/eli/sd/1889/39> sfl:hasVersion ?sv .
        ?sv eli:version_date ?dv .
      } ORDER BY DESC(?dv) LIMIT 1
    }
    ?sv eli:is_about ?c .
    ?c skos:prefLabel ?l
    # filter by lang 'fi' or 'sv'
    FILTER (lang(?l) = 'fi')
  }
</pre>
<hr/>

<p>Kysely hakee ajantasaisen lainsäädännön voimassa olevien säädösten uusimmat versiot</p>
<pre>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/&gt;
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#&gt;

  # Query : Find latest versions of statutes that are in force
  SELECT ?statuteVersion ?title WHERE {
    GRAPH &lt;http://data.finlex.fi/sd/ajantasa&gt; {
      ?statute a sfl:Statute ;
               sfl:hasVersion ?statuteVersion .
      FILTER NOT EXISTS {
        ?statute eli:in_force eli:InForce-notInForce .
      }
      OPTIONAL {
        ?statuteVersion eli:version_date ?date .
      }
      FILTER (!bound(?date) || NOT EXISTS {
        ?statute sfl:hasVersion ?statuteVersion2 .
        FILTER (?statuteVersion2 != ?statuteVersion)
        ?statuteVersion2 eli:version_date ?date2 .
        FILTER (?date2 > ?date)}
      )
      ?statuteVersion eli:is_realized_by ?statuteExpression .
      ?statuteExpression eli:language <http://publications.europa.eu/resource/authority/language/FIN> ;
                         eli:title ?title .
    }
  }
</pre>
