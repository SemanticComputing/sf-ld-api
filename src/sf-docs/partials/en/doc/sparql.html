<h1>SPARQL endpoint</h1>
<p>
  <a href="https://www.w3.org/TR/sparql11-query/">SPARQL</a> a RDF query language whose syntax resembles that of SQL. We recommend the use of Semantic Finlex datasets by making direct SPARQL queries to the SPARQL endpoint. The service point address is <b>http://data.finlex.fi/sparql</b>. You can try this by using tools such as <a href="http://yasgui.org/#query=PREFIX%20rdf%3A%20%3Chttp%3A%2F%2Fwww.w3.org%2F1999%2F02%2F22-rdf-syntax-ns%23%3E%0APREFIX%20rdfs%3A%20%3Chttp%3A%2F%2Fwww.w3.org%2F2000%2F01%2Frdf-schema%23%3E%0ASELECT%20*%20WHERE%20%7B%0A%20%20%3Fsub%20%3Fpred%20%3Fobj%20.%0A%7D%20%0ALIMIT%2010&endpoint=http%3A%2F%2Fdata.finlex.fi%2Fsparql">YASGUI</a>. Below are a few examples of SPARQL queries, with which you can ping an interface by copying the text inside the box into the YASGUI query field. In the endpoint field, type the address http://data.finlex.fi/sparql.
</p>

<hr/>

<p><a href="http://yasgui.org/short/S1LnErixb">This query</a> will retrieve the URIs of the ten oldest statutes available in the database (based on the year of the statute).</p>
<pre>
  PREFIX rdf: &lt;http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>

  # Query : List 10 of the oldest statutes
  SELECT ?s WHERE
  {
   ?s rdf:type sfl:Statute .
  } ORDER BY ?s LIMIT 10
</pre>
<hr/>

<p>The <a href="http://yasgui.org/short/rkzXrBieZ">query</a> will retrieve the different versions of the Criminal Code (39/1889)</p>
<pre>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>

  # Query : Get different temporal versions of Criminal Code
  SELECT ?document_version ?format ?content
  WHERE {
    # you can replace &lt;http://data.finlex.fi/eli/sd/1889/39> with eg. one of the following:
    # http://data.finlex.fi/eli/sd/1889/39/johtolause
    # http://data.finlex.fi/eli/sd/1889/39/luku/1
    # http://data.finlex.fi/eli/sd/1889/39/luku/1/pykala/1
   &lt;http://data.finlex.fi/eli/sd/1889/39> eli:has_member ?document_version .

   # lang options: fin,swe
   # format options: txt and html
   BIND(IRI(CONCAT(str(?document_version),"/fin/txt")) AS ?format)

   # replace sfl:text with sfl:html for html version
   ?format sfl:text ?content.
  }
</pre>
<hr/>

<p>The <a href="http://yasgui.org/short/SkntHBixb">query</a> will retrieve 10 different Finnish statute titles</p>
<pre>
  PREFIX rdf: &lt;http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>

  # Query : Get 10 legal document titles
  SELECT ?title
  WHERE {
    ?s rdf:type sfl:Statute .
    ?s eli:has_member ?v .
    BIND(IRI(CONCAT(str(?v),"/fin")) AS ?e)
    ?e eli:title ?title .
  } LIMIT 10
</pre>
<hr/>

<p>The <a href="http://yasgui.org/short/BJxsSHix-">query</a> will retrieve the latest version of the Limited Liability Companies Act (624/2006)</p>
<pre>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>

  # Query : Get the newest version of a statute (Osakeyhtiölaki)
  SELECT ?s WHERE {
    &lt;http://data.finlex.fi/eli/sd/2006/624> eli:has_member ?s .
    OPTIONAL {?s eli:version_date ?dv .}
  }
  ORDER BY DESC(?dv) LIMIT 1
</pre>
<hr/>

<p>The <a href="http://yasgui.org/short/B1d2HSsxW">query</a> will retrieve the Finnish section headings of the latest version of the Limited Liability Companies Act</p>
<pre>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>

  # Query : Get section headings of the newest version of a statute
  SELECT ?t WHERE {
    {
      SELECT ?sv ?dv WHERE
      {
        &lt;http://data.finlex.fi/eli/sd/2008/521> eli:has_member ?sv .
        OPTIONAL { ?sv eli:version_date ?dv .}
      } ORDER BY DESC(?dv) LIMIT 1
    }
    ?sv eli:has_part+ ?sec .
    ?sec a sfl:SectionVersion .
    BIND(IRI(CONCAT(str(?sec),"/fin")) AS ?exp)
    ?exp eli:title ?t .
  }
</pre>
<hr/>

<p>The <a href="http://yasgui.org/short/BkZ18HixW">query</a> will list 10 decisions of a court of law.</p>
<pre>
  PREFIX rdf: &lt;http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX sfcl: &lt;http://data.finlex.fi/schema/sfcl/>

  # Query : List judgments
  SELECT ?j WHERE
  {
   ?j rdf:type sfcl:Judgment .
 } LIMIT 10
</pre>
<hr/>

<p>The <a href="http://yasgui.org/short/B1O-Urjgb">query</a> will retrieve the Finnish textual content of a specific decision by the Supreme Court</p>
<pre>
  PREFIX sfcl: &lt;http://data.finlex.fi/schema/sfcl/>

  # Query : Get judgment content
  SELECT ?content
  WHERE {
   # lang options: fin,swe
   # format options: txt, html, xml
   # replace sfcl:text with sfcl:html / sfcl:xml for html / xml version
   &lt;http://data.finlex.fi/ecli/kko/2015/65/fin/txt> sfcl:text ?content.
  }
</pre>
<hr/>

<p>The <a href="http://yasgui.org/short/HyOQLSolb">query</a> will count the total number of different types of paragraphs in the latest version of the Criminal Code</p>
<pre>
  PREFIX rdf: &lt;http://www.w3.org/1999/02/22-rdf-syntax-ns#>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>

  # Query : Count number of different subparts of the latest statute version
  SELECT (COUNT(?o) AS ?co) ?c WHERE {
    {
    	SELECT ?sv WHERE {
        &lt;http://data.finlex.fi/eli/sd/1889/39> eli:has_member ?sv .
        ?sv eli:version_date ?dv .
      } ORDER BY DESC(?dv) LIMIT 1
    }
    ?sv eli:has_part+ ?o.
    ?o rdf:type ?c
  } GROUP BY ?c
</pre>
<hr/>

<p>The <a href="http://yasgui.org/short/SJJrIrsl-">query</a> will retrieve references to other statutes in the latest version of the Criminal Code</p>
<pre>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>

  # Query : Find references to other statutes from latest statute version
  SELECT ?o ?r WHERE {
    # select latest
    {
      SELECT ?sv WHERE {
        &lt;http://data.finlex.fi/eli/sd/1889/39> eli:has_member ?sv .
        ?sv eli:version_date ?dv .
      } ORDER BY DESC(?dv) LIMIT 1
    }
    # get all parts
    ?sv eli:has_part+ ?o.
    # get references
    ?o eli:cites ?r
  }
</pre>
<hr/>

<p>The <a href="ihttp://yasgui.org/short/rk5L8roeb">query</a> will retrieve keywords from the latest Finnish version of the Criminal Code</p>
<pre>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/>
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#>
  PREFIX skos: &lt;http://www.w3.org/2004/02/skos/core#>

  # Query : Find keywords by statute
  SELECT ?sv ?c ?l WHERE {
    {
      SELECT ?sv WHERE {
        &lt;http://data.finlex.fi/eli/sd/1889/39> eli:has_member ?sv .
        ?sv eli:version_date ?dv .
      } ORDER BY DESC(?dv) LIMIT 1
    }
    ?sv eli:is_about ?c .
    ?c skos:prefLabel ?l
    # filter by lang 'fi' or 'sv'
    FILTER (lang(?l) = 'fi')
  }
</pre>
<hr/>

<p>The <a href="http://yasgui.org/short/r1twLSslb">query</a> will retrieve the latest versions of the statutes in force</p>
<pre>
  PREFIX sfl: &lt;http://data.finlex.fi/schema/sfl/&gt;
  PREFIX eli: &lt;http://data.europa.eu/eli/ontology#&gt;

  # Query : Find latest versions of statutes that are in force
  SELECT ?statuteVersion ?title WHERE {
    GRAPH &lt;http://data.finlex.fi/eli/sd/ajantasa/&gt; {
      ?statute a sfl:Statute ;
               eli:has_member ?statuteVersion .
      FILTER NOT EXISTS {
        ?statute eli:in_force eli:InForce-notInForce .
      }
      OPTIONAL {
        ?statuteVersion eli:version_date ?date .
      }
      FILTER (!bound(?date) || NOT EXISTS {
        ?statute eli:has_member ?statuteVersion2 .
        FILTER (?statuteVersion2 != ?statuteVersion)
        ?statuteVersion2 eli:version_date ?date2 .
        FILTER (?date2 > ?date)}
      )
      ?statuteVersion eli:is_realized_by ?statuteExpression .
      ?statuteExpression eli:language &lt;http://publications.europa.eu/resource/authority/language/FIN&gt; ;
                         eli:title ?title .
    }
  }
</pre>
